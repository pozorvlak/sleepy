#!/usr/bin/perl

use strict;
use warnings;
use IO::Handle;
use Pod::Usage;
use Getopt::Std;

pod2usage(2) unless @ARGV;

my %opts;
getopts('c:p:', \%opts);
my $chunk_length = $opts{c} || 1;
my $pause_length = $opts{p} || 5;

my $PLAYER;
print STDERR "Playing...";
open($PLAYER, "|-", "mplayer", "-slave", @ARGV)
  or die "Can't start mplayer: $!";

$PLAYER->autoflush(1);

while ($PLAYER) {
  sleep $chunk_length;
# pause
  print $PLAYER "p\n";
  sleep $pause_length;
# unpause
  print $PLAYER "p\n";
}

sub HELP_MESSAGE { # called by Getopt::Std when given the --help flag
  pod2usage( { -exitval => 0, -verbose => 2 } );
}

=head1 NAME

sleepy - play video files a chunk at a time, with pauses.

=head1 SYNOPSIS

    sleepy --help
    sleepy [-c CHUNK LENGTH] [-p PAUSE LENGTH] [FILE]...
    sleepy [-c CHUNK LENGTH] [-p PAUSE LENGTH] [MPLAYER OPTION]... [FILE]...

=head1 DESCRIPTION

Play the specified FILES a chunk at a time, pausing between chunks. The default
chunk length is 1 second, and the default pause length is 5 seconds.

Requires mplayer to be installed.

All MPLAYER OPTIONs are passed directly to mplayer. See the mplayer manpage for more details on mplayer options.

=head1 AUTHOR

Miles Gould, with help from Ed Leiper and Mat Brown.

=head1 REPORTING BUGS

Please report bugs and send patches to C<miles@assyrian.org.uk>. Or (better),
fork the project on GitHub and send me a pull request when you're done.

=head1 COPYRIGHT

This program is free software, and is available under the same terms as Perl
itself.

=head1 SEE ALSO

C<mplayer(1)>, C<http://www.mplayerhq.hu>.
